buildscript {
    ext.kotlin_version = '1.2.31'
    ext.powermock_version = '1.6.4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    versionFile = project.file("VERSION")
    versionName = versionFile.readLines()[0].trim()
}

intellij {
    version = '15.0.4'
    plugins = ['github', 'git4idea']
    pluginName = 'Find Pull Request'
    updateSinceUntilBuild = false
    sameSinceUntilBuild = false
    publish {
        username 'shiraji'
        password System.getenv('PASSWORD')
        pluginId '8262'
    }
}

group 'com.github.shiraji.findpullrequest'
version versionName

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.powermock:powermock-api-mockito:$powermock_version"
    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "org.powermock:powermock-module-junit4-rule:$powermock_version"
    testCompile "org.powermock:powermock-classloading-xstream:$powermock_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task prepareForNextDevelopment << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = (versionParts[2] as int) + 1
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}-SNAPSHOT"
    versionFile.write(newVersion + "\n")
}

task prepareForRelease << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = versionParts[2].split(/-/)[0]
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}"
    versionFile.write(newVersion + "\n")
    new File("$projectDir/.travis/release").createNewFile()
}
